@Library('github.com/opspresso/pipeline@master')
def pipeline = new com.opspresso.Pipeline()
def label = "worker-${UUID.randomUUID().toString()}"
def VERSION = ""
def SOURCE_LANG = ""
def SOURCE_ROOT = ""
def BASE_DOMAIN = ""
def REGISTRY = ""
properties([
  buildDiscarder(logRotator(daysToKeepStr: "60", numToKeepStr: "30"))
])
podTemplate(label: label, containers: [
  containerTemplate(name: "builder", image: "quay.io/opspresso/builder", command: "cat", ttyEnabled: true, alwaysPullImage: true),
  containerTemplate(name: "maven", image: "maven", command: "cat", ttyEnabled: true),
  containerTemplate(name: "node", image: "node", command: "cat", ttyEnabled: true)
], volumes: [
  hostPathVolume(mountPath: "/var/run/docker.sock", hostPath: "/var/run/docker.sock"),
  hostPathVolume(mountPath: "/home/jenkins/.draft", hostPath: "/home/jenkins/.draft"),
  hostPathVolume(mountPath: "/home/jenkins/.helm", hostPath: "/home/jenkins/.helm")
]) {
  node(label) {
    stage("Checkout") {
      container("builder") {
        try {
          if (REPOSITORY_SECRET) {
            git(url: REPOSITORY_URL, branch: BRANCH_NAME, credentialsId: REPOSITORY_SECRET)
          } else {
            git(url: REPOSITORY_URL, branch: BRANCH_NAME)
          }
        } catch (e) {
          checkout_failure(IMAGE_NAME, RUN_DISPLAY_URL)
          throw e
        }

        pipeline.scan(IMAGE_NAME, BRANCH_NAME)

        VERSION = pipeline.version
        REGISTRY = pipeline.registry
        SOURCE_LANG = pipeline.source_lang
        SOURCE_ROOT = pipeline.source_root
        BASE_DOMAIN = pipeline.base_domain

        if (!SLACK_TOKEN && pipeline.slack_token) {
          SLACK_TOKEN = pipeline.slack_token
        }
      }
    }
    stage("Build") {
      if (SOURCE_LANG == "java") {
        container("maven") {
          try {
            sh """
              cd $SOURCE_ROOT
              mvn package -s /home/jenkins/.m2/settings.xml
            """
            build_success(IMAGE_NAME, VERSION, RUN_DISPLAY_URL)
          } catch (e) {
            build_failure(IMAGE_NAME, RUN_DISPLAY_URL)
            throw e
          }
        }
      }
      else if (SOURCE_LANG == "nodejs") {
        container("node") {
          try {
            sh """
              cd $SOURCE_ROOT
              npm run build
            """
            build_success(IMAGE_NAME, VERSION, RUN_DISPLAY_URL)
          } catch (e) {
            build_failure(IMAGE_NAME, RUN_DISPLAY_URL)
            throw e
          }
        }
      }
      else {
        sh """
          echo "skipped."
        """
      }
    }
    if (BRANCH_NAME != "master") {
      stage("Development") {
        container("builder") {
          def NAMESPACE = "development"

          pipeline.draft_up(IMAGE_NAME, NAMESPACE, CLUSTER)

          deploy_success(IMAGE_NAME, VERSION, NAMESPACE, BASE_DOMAIN)
        }
      }
    }
    if (BRANCH_NAME == "master") {
      stage("Build Image") {
        parallel(
          "Build Docker": {
            container("builder") {

              pipeline.build_image(IMAGE_NAME, VERSION)

            }
          },
          "Build Charts": {
            container("builder") {

              pipeline.build_chart(IMAGE_NAME, VERSION)

            }
          }
        )
      }
      stage("Staging") {
        container("builder") {
          def NAMESPACE = "staging"

          pipeline.helm_install(IMAGE_NAME, VERSION, NAMESPACE, CLUSTER)

          deploy_success(IMAGE_NAME, VERSION, NAMESPACE, BASE_DOMAIN)
        }
      }
      stage("Confirm") {
        container("builder") {
          def NAMESPACE = "production"
          deploy_confirm(IMAGE_NAME, VERSION, NAMESPACE, RUN_DISPLAY_URL)
          timeout(time: 60, unit: "MINUTES") {
            input(message: "$IMAGE_NAME $VERSION to $NAMESPACE")
          }
        }
      }
      stage("Production") {
        container("builder") {
          def NAMESPACE = "production"

          pipeline.helm_install(IMAGE_NAME, VERSION, NAMESPACE, CLUSTER)

          deploy_success(IMAGE_NAME, VERSION, NAMESPACE, BASE_DOMAIN)
        }
      }
    }
  }
}
def checkout_failure(IMAGE_NAME, RUN_DISPLAY_URL) {
  notify("danger", "Checkout Failure", "`$IMAGE_NAME`", "$env.JOB_NAME <$RUN_DISPLAY_URL|#$env.BUILD_NUMBER>")
}
def build_failure(IMAGE_NAME, RUN_DISPLAY_URL) {
  notify("danger", "Build Failure", "`$IMAGE_NAME`", "$env.JOB_NAME <$RUN_DISPLAY_URL|#$env.BUILD_NUMBER>")
}
def build_success(IMAGE_NAME, VERSION, RUN_DISPLAY_URL) {
  notify("good", "Build Success", "`$IMAGE_NAME` `$VERSION` :heavy_check_mark:", "$env.JOB_NAME <$RUN_DISPLAY_URL|#$env.BUILD_NUMBER>")
}
def deploy_confirm(IMAGE_NAME, VERSION, STAGE, RUN_DISPLAY_URL) {
  notify("warning", "Deply Confirm", "`$IMAGE_NAME` `$VERSION` :rocket: `$STAGE`", "$env.JOB_NAME <$RUN_DISPLAY_URL|#$env.BUILD_NUMBER>")
}
def deploy_success(IMAGE_NAME, VERSION, STAGE, BASE_DOMAIN) {
  def SEE="https://$IMAGE_NAME-$STAGE.$BASE_DOMAIN"
  notify("good", "Deply Success", "`$IMAGE_NAME` `$VERSION` :satellite: `$STAGE`", "see <$SEE|$env.IMAGE_NAME-$STAGE>")
}
def notify(COLOR, TITLE, MESSAGE, FOOTER) {
  try {
    if (SLACK_TOKEN) {
      sh """
        curl -sL toast.sh/helper/slack.sh | bash -s -- --token='$SLACK_TOKEN' \
             --color='$COLOR' --title='$TITLE' --footer='$FOOTER' '$MESSAGE'
      """
    }
  } catch (ignored) {
  }
}
